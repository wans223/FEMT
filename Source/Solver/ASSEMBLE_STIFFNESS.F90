!***************************************************************************************************
!- PURPORSE:
!     ASSEMBLE ELEMENT STIFFNESS MATRIX INTO GLOBAL STIFFNESS MATRIX (SPARSE, COLUMN-WISE VARIABLE BANDWIDTH).
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NON
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: ERROR, NUM2STR
!
!- CALLED BY:
!  SOLVE_MECH_STATIC_DIR  
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!
!- MODIFIED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JULY 26, 2022
!***************************************************************************************************

SUBROUTINE ASSEMBLE_STIFFNESS

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, ELE_STIFF, NUM_NODE, NUM_ELEMENT, ELEMENT_LIB, &
                         ELEMENTS, NUM_DISP_EQUATION, NODE_DISP_DOF, STIFFNESS, DIAGONAL, ERROR, NUM2STR 
                          
IMPLICIT NONE

INTEGER(INT_KIND)              I_ND, J_ND        ! LOOP INDEX FOR NODE
INTEGER(INT_KIND)              I_ELE             ! LOOP INDEX FOR ELEMENT
INTEGER(INT_KIND)              I_DOF, J_DOF      ! INDEX FOR NODE'S DOFS
INTEGER(INT_KIND)              MIN_AJNO          ! THE MINIMUM ADJACENT NODE NO. AROUND A NODE
INTEGER(INT_KIND)              E_NODE            ! THE NUMBER OF ELEMENT NODES
INTEGER(INT_KIND)              DOFS              ! THE NUMBER OF DOFS OF A NODE
INTEGER(INT_KIND)              HEIGHT            ! THE MINIMUM COLUMN HIGHT - 1
INTEGER(INT_KIND)              NSTIFF            ! THE NUMBER OF STIFF VECTOR ELEMENTS
INTEGER(INT_KIND)              I_E_COL           ! ELEMENT COLUMN NO. OF NODE I
INTEGER(INT_KIND)              I_G_COL           ! GLOBAL COLUMN NO. OF NODE I
INTEGER(INT_KIND)              J_E_ROW           ! ELEMENT ROW NO. OF NODE J
INTEGER(INT_KIND)              J_G_ROW           ! GLOBAL ROW NO. OF NODE J
INTEGER(INT_KIND)              EP                ! THE POSITION IN ELEMENT STIFFNESS MATRIX
INTEGER(INT_KIND)              GP                ! THE POSITION IN GLOBAL STIFFNESS MATRIX
INTEGER(INT_KIND)              ERR               ! ERROR INDEX
INTEGER(INT_KIND)              a
REAL(REAL_KIND), ALLOCATABLE:: ELE_STIFFNESS(:)  ! THE UPPER TRIANGLE OF ELEMENT STIFFNESS MATRIX


!-- CONSTRUCT THE DIAGONAL POSITION VECTOR AND ALLOCATE THE GLOBAL STIFFNESS VECTOR --
ALLOCATE(DIAGONAL(NUM_DISP_EQUATION + 1), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate DIAGONAL!')
DIAGONAL(1) = 1
DO I_ND = 1, NUM_NODE
   ! SEARCH FOR THE MINIMUM ADJACENT NODE NO. AROUND THIS NODE
   MIN_AJNO = NUM_NODE + 1
   DO I_ELE = 1, NUM_ELEMENT         
      IF(ANY(ELEMENTS(I_ELE)%NODE .EQ. I_ND)) THEN
         IF(MINVAL(ELEMENTS(I_ELE)%NODE) .LT. MIN_AJNO) THEN
            MIN_AJNO = MINVAL(ELEMENTS(I_ELE)%NODE)         
            DOFS     = ELEMENT_LIB(ELEMENTS(I_ELE)%ELEMENT)%DISP_DOF
         ENDIF
      ENDIF
   ENDDO

   ! ASSIGN VALUES TO THE DIAGONAL VECTOR
   HEIGHT = (I_ND - MIN_AJNO) * DOFS                           ! THE MINIMUM COLUMN HIGHT - 1
   DO I_DOF = 1, DOFS                                          ! EACH NODE OCCUPIES DOFS COLUMNS
      J_DOF           = NODE_DISP_DOF(I_ND)%DOFS(I_DOF) + 1    ! THE GLOBAL DOF NO. + 1
      DIAGONAL(J_DOF) = DIAGONAL(J_DOF - 1) + HEIGHT + I_DOF
   ENDDO
ENDDO

NSTIFF = DIAGONAL(NUM_DISP_EQUATION + 1) - 1
ALLOCATE(STIFFNESS(NSTIFF), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate STIFFNESS!')
STIFFNESS = 0.0

!-- ASSEMBLE THE STIFFNESS MATRIX --
DO I_ELE = 1, NUM_ELEMENT
   ! READ ELEMENT STIFFNESS
   ALLOCATE(ELE_STIFFNESS(ELEMENT_LIB(ELEMENTS(I_ELE)%ELEMENT)%NUM_STIFF), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Fail to create ELE_STIFFNESS!')
   READ(ELE_STIFF, REC = I_ELE) ELE_STIFFNESS ! UPPER TRIANGLE
    
   E_NODE = ELEMENT_LIB(ELEMENTS(I_ELE)%ELEMENT)%NUM_NODE
   DOFS   = ELEMENT_LIB(ELEMENTS(I_ELE)%ELEMENT)%DISP_DOF
   DO I_ND = 1, E_NODE  ! FOR EACH ELEMENT NODE
      ! EACH NODE'S DEGREES OF FREEDOM
      DO I_DOF = 1, DOFS
         I_E_COL = (I_ND - 1) * DOFS + I_DOF
         I_G_COL = NODE_DISP_DOF(ELEMENTS(I_ELE)%NODE(I_ND))%DOFS(I_DOF)
         
         ! CONNECTION WITH OTHER NODES IN THE SAME ELEMENT
         DO J_ND = 1, E_NODE
            ! EACH NODE'S DEGREES OF FREEDOM
            DO J_DOF = 1, DOFS 
               J_E_ROW = (J_ND - 1) * DOFS + J_DOF
               J_G_ROW = NODE_DISP_DOF(ELEMENTS(I_ELE)%NODE(J_ND))%DOFS(J_DOF)

               ! DISCARD LOWER TIANGLE ELEMENTS                         
               IF(J_G_ROW .LE. I_G_COL) THEN
                  IF(J_E_ROW .GT. I_E_COL) THEN
                     EP = (E_NODE * DOFS * 2 - I_E_COL + 2) * (I_E_COL - 1) / 2 + 1 + (J_E_ROW - I_E_COL)  !(EDITED BY YIXIAO SUN)
                  ELSE
                     EP = (E_NODE * DOFS * 2 - J_E_ROW + 2) * (J_E_ROW - 1) / 2 + 1 + (I_E_COL - J_E_ROW)  !(EDITED BY YIXIAO SUN)
                  ENDIF                  
                  GP = DIAGONAL(I_G_COL) + (I_G_COL - J_G_ROW)
                  STIFFNESS(GP) = STIFFNESS(GP) + ELE_STIFFNESS(EP)
               ENDIF
            ENDDO ! J_DOF
         ENDDO ! J_ND
      ENDDO ! I_DOF
   ENDDO  ! I_ND
   
   DEALLOCATE(ELE_STIFFNESS)
ENDDO  ! I_ELE

!-- CHECK THE STIFF MATRIX --
a = MINVAL(STIFFNESS(DIAGONAL(1:NUM_DISP_EQUATION)))
IF(MINVAL(STIFFNESS(DIAGONAL(1:NUM_DISP_EQUATION))) .LE. 0.0) &
   CALL ERROR('Diagonal value <= 0 in global stiffness in column ' // TRIM(NUM2STR(MINLOC(STIFFNESS(DIAGONAL(1:NUM_DISP_EQUATION)),1))) //'!')

END SUBROUTINE ASSEMBLE_STIFFNESS