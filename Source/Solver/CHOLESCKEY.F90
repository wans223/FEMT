!***************************************************************************************************
!- PURPORSE:
!     FACTORIZE THE STIFFNESS MATRIX WITH THE IMPROVED CHOLESCKEY METHOD.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : ERROR, NUM2STR
!
!- CALLED BY:
!  SOLVE_MECH_STATIC_DIR 
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE CHOLESCKEY

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, NUM_DISP_EQUATION, STIFFNESS, DIAGONAL, FORCE, DISPLACEMENT, ERROR, NUM2STR

IMPLICIT NONE

INTEGER(INT_KIND) I_COL, J_COL   ! INDEX FOR COLUMN NUMBER
INTEGER(INT_KIND) DIA_I, DIA_J   ! DIAGONAL POSITION IN STIFFNESS
INTEGER(INT_KIND) KL             ! THE FIRST ELEMEMT'S POSITION IN THIS COLUMN
INTEGER(INT_KIND) KU             ! THE LAST NONZERO ELEMENT'S POSITION IN THIS COLUMN
INTEGER(INT_KIND) KH             ! COLUMN HIGHT - 2
INTEGER(INT_KIND) IC             ! THE NUMBER OF TREATED ELEMENTS IN CURRENT COLUMN
INTEGER(INT_KIND) IP             ! THE PRONE TO BE TREATED ELEMENTS IN CURRENT COLUMN
INTEGER(INT_KIND) ND             ! COLUMN HIGHT - 1
INTEGER(INT_KIND) MI             ! CURRENT PRONE TO BE TREATED ELEMENT'S POSITION
INTEGER(INT_KIND) MJ             ! FORWARD ELEMENT'S POSITION
INTEGER(INT_KIND) I, J, N        ! LOOP INDEX
REAL(REAL_KIND)   S              ! THE TEMPERARY SUM FOR ELIMINATION
REAL(REAL_KIND)   TEMP           ! THE TEMPERARY FACTOR FOR ELIMINATION

!-- DECOMPOSE THE COEFFICIENT MATRIX --
DO I_COL = 1, NUM_DISP_EQUATION 
   DIA_I = DIAGONAL(I_COL)       ! ELEMENT (I_COL,I_COL)'S POSITION
   KL = DIA_I + 1                ! THE FIRST ELEMEMT'S POSITION
   KU = DIAGONAL(I_COL + 1) - 1  ! THE LAST NONZERO ELEMENT'S POSITION
   KH = KU - KL                  ! COLUMN HIGHT - 2

   ! IF THERE ARE AT LEST 3 ELEMENTS IN THIS COLUMN
   IF(KH .GT. 0) THEN 
      J_COL = I_COL - KH   ! FORWARD COLUMN NO.
      IC    = 0            ! THE NUMBER OF TREATED ELEMENTS IN CURRENT COLUMN
      IP    = KU           ! THE PRONE TO BE TREATED ELEMENTS IN CURRENT COLUMN
      DO I = 1, KH
         IC    = IC + 1
         IP    = IP - 1
         DIA_J = DIAGONAL(J_COL)    ! ELEMENT (J_COL,J_COL)'S POSITION
         ND = DIAGONAL(J_COL + 1) - DIA_J - 1 ! ND : COLUMN HIGHT - 1
         IF(ND .GT. 0) THEN
            S = 0.0
            DO J = 1, MIN(IC,ND) ! THE SHORTER BAND
               MJ = DIA_J + J    ! FORWARD ELEMENT'S POSITION
               MI = IP + J       ! CURRENT PRONE TO BE TREATED ELEMENT'S POSITION
               S = S + STIFFNESS(MI) * STIFFNESS(MJ)
            ENDDO
            STIFFNESS(IP) = STIFFNESS(IP) - S
         ENDIF
         J_COL = J_COL + 1
      ENDDO !I
   ENDIF

   IF(KH .GE. 0) THEN
      J_COL = I_COL
      S = 0.0
      DO I = KL, KU
         J_COL = J_COL - 1
         DIA_J = DIAGONAL(J_COL)
         TEMP  = STIFFNESS(I) / STIFFNESS(DIA_J)
         S     = S + TEMP * STIFFNESS(I)
         STIFFNESS(I) = TEMP
      ENDDO
      STIFFNESS(DIA_I) = STIFFNESS(DIA_I) - S
   ENDIF

   ! CHECK THE DIAGONAL ELEMENT
   IF(STIFFNESS(DIA_I) .LE. 0) &
      CALL ERROR('Stiff matrix is not positive definited in equation ' // TRIM(NUM2STR(I_COL)) &
                 // ' with the diagonal value ' // TRIM(NUM2STR(STIFFNESS(DIA_I))) // '!')
ENDDO ! I_COL

!-- BACK SUBSTITUED --
DO I_COL = 1, NUM_DISP_EQUATION
   KL = DIAGONAL(I_COL) + 1
   KU = DIAGONAL(I_COL + 1) - 1
   IF(KU - KL .GE. 0) THEN
      I = I_COL
      S = 0.0
      DO J = KL, KU
         I = I - 1
         S = S + STIFFNESS(J) * FORCE(I)
      ENDDO
      FORCE(I_COL) = FORCE(I_COL) - S
   ENDIF
ENDDO

FORCE = FORCE / STIFFNESS(DIAGONAL(1:NUM_DISP_EQUATION))

IF(NUM_DISP_EQUATION .GT. 1) THEN
   N = NUM_DISP_EQUATION
   DO I_COL = 2, NUM_DISP_EQUATION
      KL = DIAGONAL(N) + 1
      KU = DIAGONAL(N + 1) - 1
      IF(KU - KL .GE. 0) THEN
         I = N
         DO J = KL, KU
            I = I - 1
            FORCE(I) = FORCE(I) - STIFFNESS(J) * FORCE(N)
         ENDDO
      ENDIF
      N = N - 1
   ENDDO
ENDIF

DISPLACEMENT = FORCE
   
END SUBROUTINE CHOLESCKEY