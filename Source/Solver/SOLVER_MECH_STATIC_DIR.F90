!***************************************************************************************************
!- PURPORSE:
!     SOLVE STATICAL MECHANICAL PROBLEM WITH VARIABLE BANDWIDTH GAUSS ELIMINATION SOLVER.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  EXTERNAL PROCEDURE: ASSEMBLE_STIFFNESS, CHOLESCKEY
!
!- CALLED BY:
!  SOLVE  
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE SOLVE_MECH_STATIC_DIR

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, NODE_DISP_DOF, NUM_GIVEN_DISP, GIVEN_DISP, FORCE, STIFFNESS, DIAGONAL
                         
IMPLICIT NONE

INTEGER(INT_KIND)            I               ! LOOP INDEX
INTEGER(INT_KIND)            J               ! LOOP INDEX
INTEGER(INT_KIND)            DOF             ! THE DOF NUMBER OF A GIVEN DISPLACEMENT
INTEGER(INT_KIND)            POS             ! THE POSITION OF DIAGONAL ELEMENT
REAL(REAL_KIND), PARAMETER:: LARGE = 1.0E30  ! THE LARGE NUMBER FOR ADDING GIVEN DISPLACEMENT


!-- ASSEMBLE STIFFNESS MATRIX --
CALL ASSEMBLE_STIFFNESS

!-- ADD DISPLACEMENT BOUNDARY CONDITION --
DO I = 1, NUM_GIVEN_DISP
   DO J = 1, NODE_DISP_DOF(GIVEN_DISP(I)%NODE)%NUM_DOF
      IF(GIVEN_DISP(I)%GIVEN(J)) THEN
         DOF = NODE_DISP_DOF(GIVEN_DISP(I)%NODE)%DOFS(J)
         POS = DIAGONAL(DOF)
         STIFFNESS(POS) = STIFFNESS(POS) * LARGE        
         FORCE(DOF)     = STIFFNESS(POS) * GIVEN_DISP(I)%VALUE(J)
      ENDIF
   ENDDO      
ENDDO

!-- SOLVE EQUATION TO OBTAIN DISPLACEMENTS --
CALL CHOLESCKEY
   
END SUBROUTINE SOLVE_MECH_STATIC_DIR