!***************************************************************************************************
!- PURPORSE:
!     CALCULATE ELEMENT LOAD VECTOR FOR PLANE ELEMENTS WHEN APPLYING DISTRIBUTED LINE LOAD.
!  
!- INPUT ARGUMENTS:
!  LINE_FORCE: THE STRUCTURE OF LINE LOAD
!  NUM_NODE  : THE NUMBER OF NODES PER LINE OF THIS ELEMENT, WHICH IS EQUAL TO THE NUMBER OF INTEGRATION POINTS
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  NONE
!
!- CALLED BY
!  GET_FORCE
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE LINE_FORCE_PLANE(LINE_FORCE, NUM_NODE)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, BND_LINE, XYZ, ELEMENTS, ELEMENT_LIB, GEOMETRIES, LINE_SHAPE2, &
                         LINE_SHAPE3, LINE_DSHAPE2, LINE_DSHAPE3, ELE_FORCE, GAUSS_WEIGHT2, GAUSS_WEIGHT3

IMPLICIT NONE

TYPE(BND_LINE), INTENT(IN)::    LINE_FORCE           ! THE STRUCTURE OF LINE LOAD
INTEGER(INT_KIND), INTENT(IN):: NUM_NODE             ! THE NUMBER OF NODES (INTEGRATION POINTS) PER LINE OF THIS ELEMENT
INTEGER(INT_KIND)               I, J, K              ! LOOP INDEX
INTEGER(INT_KIND)               ELE_NO               ! ELEMENT NUMBER
INTEGER(INT_KIND)               POS                  ! THE POSITION IN ELEMENT LOAD VECTOR
REAL(REAL_KIND)                 TRACTION(2)          ! NORMAL AND TANGENT TRACTIONS
REAL(REAL_KIND)                 F(2)                 ! NODAL FORCE IN X AND Y DIRECTION
REAL(REAL_KIND)                 D_KESI(2)            ! dX/dKESI, dY/dKESI
REAL(REAL_KIND)                 LINE_XY(2,NUM_NODE)  ! X AND Y COORDINATES OF NODES ON THIS LINE
REAL(REAL_KIND)                 THICKNESS            ! ELEMENT THICKNESS

!-- PREPARATION --
! GET THE COORDINATE OF LINE NODES
DO I = 1, NUM_NODE
   LINE_XY(1:2,I) = XYZ(1:2,LINE_FORCE%NODE(I))
ENDDO 

! GET ELEMENT THICKNESS
ELE_NO    = LINE_FORCE%ELEMENT
THICKNESS = GEOMETRIES(ELEMENTS(ELE_NO)%GEOMETRY)%THICKNESS

!-- CALCULATE ELEMENT FORCE VECTOR --
DO I = 1, NUM_NODE  ! LOOP FOR EACH INTEGRATION POINT
   ! CALCULATE NORMAL TRACTION, TANGENT TRACTION AND dX/dKSI, dY/dKSI AT THIS INTEGRATION POINT
   IF(NUM_NODE .EQ. 2) THEN
      TRACTION = MATMUL(LINE_FORCE%TRACTION(1:2,1:NUM_NODE), LINE_SHAPE2(:,I))
      D_KESI   = MATMUL(LINE_XY(:,1:NUM_NODE), LINE_DSHAPE2(:,I))
   ELSE
      TRACTION = MATMUL(LINE_FORCE%TRACTION(1:2,1:NUM_NODE), LINE_SHAPE3(:,I))
      D_KESI   = MATMUL(LINE_XY(:,1:NUM_NODE),LINE_DSHAPE3(:,I))   
   ENDIF
    
   ! CALCULATE FX AND FY AT THIS INTEGRATION POINT
   F(1) = -TRACTION(1) * D_KESI(2) + TRACTION(2) * D_KESI(1)
   F(2) =  TRACTION(1) * D_KESI(1) + TRACTION(2) * D_KESI(2)
   
   ! CALCULATE NODAL FORCE FOR EACH LOADED NODE
   DO J = 1, NUM_NODE
      ! GET THE ELEMENT NODE NUMBER
      DO K = 1, ELEMENT_LIB(ELEMENTS(ELE_NO)%ELEMENT)%NUM_NODE
         IF(LINE_FORCE%NODE(J) .EQ. ELEMENTS(ELE_NO)%NODE(K)) EXIT
      ENDDO
      
      POS = (K - 1) * 2 + 1
      
      IF(NUM_NODE .EQ. 2) THEN
         ELE_FORCE(ELE_NO)%VALUE(POS    ) = ELE_FORCE(ELE_NO)%VALUE(POS    ) + &
                                            LINE_SHAPE2(J,I) * F(1) * THICKNESS * GAUSS_WEIGHT2(I)
         ELE_FORCE(ELE_NO)%VALUE(POS + 1) = ELE_FORCE(ELE_NO)%VALUE(POS + 1) + &
                                            LINE_SHAPE2(J,I) * F(2) * THICKNESS * GAUSS_WEIGHT2(I)
      ELSE
         ELE_FORCE(ELE_NO)%VALUE(POS    ) = ELE_FORCE(ELE_NO)%VALUE(POS    ) + &
                                            LINE_SHAPE3(J,I) * F(1) * THICKNESS * GAUSS_WEIGHT3(I)
         ELE_FORCE(ELE_NO)%VALUE(POS + 1) = ELE_FORCE(ELE_NO)%VALUE(POS + 1) + &
                                            LINE_SHAPE3(J,I) * F(2) * THICKNESS * GAUSS_WEIGHT3(I)
      ENDIF     
   ENDDO
ENDDO

END SUBROUTINE LINE_FORCE_PLANE