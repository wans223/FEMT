!***************************************************************************************************
!- PURPORSE:
!     BRANCH TO DIFFERENT SUBROUTINE TO GET ELEMENT FORCE VECTOR.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : ERROR
!  EXTERNAL PROCEDURE: LINE_FORCE_PLANE, ASSEM_FORCE
!
!- CALLED BY
!  SOLVER_MANAGER
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE GET_FORCE

USE SOLUTION_DATA, ONLY: INT_KIND, NUM_ELEMENT, NUM_NODAL_FORCE, NUM_LINE_FORCE, ELEMENT_LIB, &
                         ELEMENTS, NODE_DISP_DOF, NODAL_FORCE, LINE_FORCE, ELE_FORCE, FORCE, ERROR

IMPLICIT NONE

INTEGER(INT_KIND) I          ! LOOP INDEX
INTEGER(INT_KIND) J          ! LOOP INDEX
INTEGER(INT_KIND) NODE
INTEGER(INT_KIND) DOFS
INTEGER(INT_KIND) ELE_TYPE   ! ELEMENT TYPE NUMBER

!-- CALCULATE ELEMENT FORCE VECTOR --
! GIVEN LINE FORCE
DO I = 1, NUM_LINE_FORCE
   ELE_TYPE = ELEMENTS(LINE_FORCE(I)%ELEMENT)%ELEMENT
   SELECT CASE(TRIM(ELEMENT_LIB(ELE_TYPE)%ELEMENT_TYPE))
      CASE ('TRIANGLE3', 'RECTANGLE4')
         CALL LINE_FORCE_PLANE(LINE_FORCE(I), 2)
        
      CASE ('TRIANGLE6', 'RECTANGLE8', 'RECTANGLE9')
         CALL LINE_FORCE_PLANE(LINE_FORCE(I), 3)
      
      CASE DEFAULT
		   CALL ERROR('Invalid element type ' // TRIM(ELEMENT_LIB(ELE_TYPE)%ELEMENT_TYPE) // '!')
   END SELECT
ENDDO

!-- ASSEMBLE GLOABLE FORCE VECTOR --
! CLEAR GLOBAL FORCE VECTOR
FORCE = 0.0

! ASSEMBLE GIVEN NODAL FORCE
DO I = 1, NUM_NODAL_FORCE                     
   NODE = NODAL_FORCE(I)%NODE
   WHERE(NODAL_FORCE(I)%GIVEN) FORCE(NODE_DISP_DOF(NODE)%DOFS) = NODAL_FORCE(I)%VALUE   
ENDDO

! ASSEMBLE ELEMENT FORCE VECTOR
DO I = 1, NUM_ELEMENT        
   ELE_TYPE = ELEMENTS(I)%ELEMENT          
   DO J = 1, ELEMENT_LIB(ELE_TYPE)%NUM_NODE
      NODE = ELEMENTS(I)%NODE(J)
      DOFS = ELEMENT_LIB(ELE_TYPE)%DISP_DOF
      FORCE(NODE_DISP_DOF(NODE)%DOFS) = FORCE(NODE_DISP_DOF(NODE)%DOFS) + &
                                        ELE_FORCE(I)%VALUE((J - 1) * DOFS + 1:J * DOFS)
   ENDDO
ENDDO

END SUBROUTINE GET_FORCE