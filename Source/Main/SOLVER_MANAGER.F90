!***************************************************************************************************
!- PURPORSE:
!     BRANCH TO DIFFERENT FUNCTION UNITS ACCORDING TO THE INSTRUCTION.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE:   ERROR
!  EXTERNAL PROCEDURE: READ_INSTRUCTION, STIFFNESS, BOUNDARY_CONDITION, GET_FORCE, SOVLE, GET_STRAIN
!                      GET_STRESS, OUTPUT, CLEAR_INSTRUCTION
!
!- CALLED BY:
!  FEMT
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE SOLVER_MANAGER

USE SOLUTION_DATA, ONLY: INSTRUCTION_NODE, INSTRUCTIONS, ERROR

IMPLICIT NONE  

TYPE(INSTRUCTION_NODE), POINTER:: P_INSTRUCTION

!-- SOLUTION BLOCK --
! READ INSTRUCTIONS
CALL READ_INSTRUCTION             
      
! EXCUTE EACH INSTRUCTION
P_INSTRUCTION => INSTRUCTIONS%HEAD         
DO WHILE(ASSOCIATED(P_INSTRUCTION))
   SELECT CASE(TRIM(P_INSTRUCTION%INSTRUCTION))            
      CASE ('STIFFNESS')
         CALL STIFFNESS(P_INSTRUCTION%OPTION)

      CASE ('BOUNDARY')
         CALL BOUNDARY_CONDITION 
         
      CASE ('FORCE')
         CALL GET_FORCE                  
            
      CASE ('SOLVE')
         CALL SOLVE(P_INSTRUCTION%OPTION)
            
      CASE ('STRAIN')
          CALL GET_STRAIN(P_INSTRUCTION%OPTION(1))
          
      CASE('STRESS')
         CALL GET_STRESS(P_INSTRUCTION%OPTION)
            
      CASE ('OUTPUT')
         CALL OUTPUT(P_INSTRUCTION%OPTION)
            
      CASE DEFAULT
         CALL ERROR('Invalid command: ' // TRIM(P_INSTRUCTION%INSTRUCTION) // ' in SOLUTION!')
   END SELECT
      
   P_INSTRUCTION => P_INSTRUCTION%NEXT
ENDDO
      
! CLEAR INSTRUCTION LIST
CALL CLEAR_INSTRUCTION
      
END SUBROUTINE SOLVER_MANAGER