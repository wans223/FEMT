!***************************************************************************************************
!- PURPORSE:
!     READ GEOMETRIC INFORMATION FROM INPUT FILE.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: GET_MACRO, ERROR, REMOVE_FIRSTWORD, NUM2STR
!
!- CALLED BY:
!  INITIATE
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE READ_GEOMETRIES

USE BASIC_DATA, ONLY: INT_KIND, REAL_KIND, WORD_KIND, LINE_KIND, CONTROL, NUM_GEOMETRY, GEOMETRIES, &
                      REMOVE_FIRSTWORD, NUM2STR, GET_MACRO, ERROR

IMPLICIT NONE

CHARACTER(LINE_KIND) COMMAND
CHARACTER(LINE_KIND) STRING           ! SOME KEYWORDS OR NUMBERS IN MACRO
CHARACTER(WORD_KIND) GEOMETRY_TYPE
INTEGER(INT_KIND)    ERR              ! ERR INDEX
INTEGER(INT_KIND)    NO               ! MATERIAL NUMBER
REAL(REAL_KIND)      VALUE            ! INPUT GEOMETRY VALUES

!-- ALLOCATE GEOMETRIES --
IF(NUM_GEOMETRY .LE. 0) CALL ERROR('NUM_GEOMETRY should be greater than 0!')
ALLOCATE(GEOMETRIES(NUM_GEOMETRY), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate GEOMETRIES!')

!-- READ PROPERTIES FOR EACH GEOMETRY --
DO
   COMMAND = GET_MACRO(CONTROL)
     
   IF(INDEX(COMMAND, 'GEOMETRY') .EQ. 1) THEN
      ! READ THE GEOMETRY NUMBER AND TYPE
      READ(COMMAND(9:),*) NO, STRING, GEOMETRY_TYPE
      IF(NO .LT. 1 .OR. NO .GT. NUM_GEOMETRY) CALL ERROR('Invalid geometry No. : ' // NUM2STR(NO))
      
      GEOMETRIES(NO)%GEOMETRY_TYPE = TRIM(GEOMETRY_TYPE)
      
      ! READ GEOMETRY PROPERTIES
      COMMAND = GET_MACRO(CONTROL)
      DO WHILE(TRIM(COMMAND) .NE. 'END GEOMETRY')
         DO WHILE(LEN_TRIM(COMMAND) .GT. 0)
            READ(COMMAND,*) STRING, VALUE
   
            SELECT CASE(TRIM(GEOMETRY_TYPE))
                CASE ('PLANE')
                  IF(TRIM(STRING) .NE. 'THICKNESS') CALL ERROR("Invalid geometry property '" // TRIM(STRING) // " '!")
                  COMMAND = REMOVE_FIRSTWORD(COMMAND) ! REMOVE 'THICKNESS'
                  READ(COMMAND,*) GEOMETRIES(NO)%THICKNESS 
                  
               CASE DEFAULT
                  CALL ERROR("Invalid geometry type '" // TRIM(GEOMETRY_TYPE) // " '!")
            END SELECT
                                       
            COMMAND = REMOVE_FIRSTWORD(COMMAND)
            COMMAND = REMOVE_FIRSTWORD(COMMAND)
         ENDDO               
            
         COMMAND = GET_MACRO(CONTROL)
      ENDDO                 
   ELSE IF(TRIM(COMMAND) .EQ. 'END GEOMETRIES') THEN
      EXIT
   ELSE
      CALL ERROR("Invalid command: '" // TRIM(COMMAND) // " ' in GEOMETRY BLOCK!")
   ENDIF
ENDDO

END SUBROUTINE READ_GEOMETRIES