!***************************************************************************************************
!- PURPORSE:
!     MAKE SOME PREPARATIONS FOR FURTHER PROCESSING THE PROGRAM.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE:   GET_MACRO, ERROR, REMOVE_FIRSTWORD
!  EXTERNAL PROCEDURE: INIT_ELE_LIB, CHECK_DATA, INIT_SOLUTION, READ_XYZ, READ_ELEMENTS, 
!                      READ_ELEMENT_TYPE, READ_MATERIALS
!
!- CALLED BY:
!  FEMT
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE INITIATE

USE SOLUTION_DATA

IMPLICIT NONE

LOGICAL(LG_KIND)     EXISTED    ! A LOGICAL VARIABLE TO INDICATE THE FILE STATUS
CHARACTER(LINE_KIND) COMMAND    ! MACRO
CHARACTER(WORD_KIND) ITEM       ! A KEY WORD IN A MACRO
INTEGER(INT_KIND)    ARGC       ! THE NUMBER OF COMMOND ARGUMENT

!-- INPUT THE PROJECT NAME --
ARGC = COMMAND_ARGUMENT_COUNT()
IF (ARGC .GT. 0) THEN
   CALL GET_COMMAND_ARGUMENT(ARGC, PROJECT_NAME)
ELSE
   PRINT '(1X,A$)', 'Project name: '
   READ *, PROJECT_NAME
ENDIF

!-- SET THE START TIME --
CALL DATE_AND_TIME (VALUES = START_TIME)
CALL SYSTEM_CLOCK(START_CLK)

!-- OPEN FILES --
! LOG FILE
OPEN(UNIT   = LOG,     &
     ACTION = 'WRITE', &
     ERR    = 10,      &
     FILE   = TRIM(PROJECT_NAME) // '.LOG')
     
! CONTROL FILE
INQUIRE(FILE = TRIM(PROJECT_NAME) // '.CTR', EXIST = EXISTED)
IF(EXISTED) THEN
   OPEN(UNIT     = CONTROL,  &
        ACTION   = 'READ',   &
        ERR      = 20,       &
        BUFFERED = 'YES',    &!!!!!!!!!!!!!!!!!!!!!!
        FILE     = TRIM(PROJECT_NAME) // '.CTR')
ELSE
   CALL ERROR(TRIM(PROJECT_NAME) // '.CTR' // ' does not existed!')
ENDIF

! BOUNDARY CONDITION FILE
INQUIRE(FILE = TRIM(PROJECT_NAME) // '.BND', EXIST = EXISTED)
IF(EXISTED) THEN
   OPEN(UNIT     = BOUNDARY, &
        ACTION   = 'READ',   &
        ERR      = 30,       &
        BUFFERED = 'YES',    &
        FILE     = TRIM(PROJECT_NAME) // '.BND')
ELSE
   CALL ERROR(TRIM(PROJECT_NAME) // '.BND' // ' does not existed!')
ENDIF

! OUTPUT FILE
OPEN(UNIT     = OUT,     &
     ACTION   = 'WRITE', &
     ERR      = 40,      &
     BUFFERED = 'YES',   &
     FILE = TRIM(PROJECT_NAME) // '.OUT')

! ELEMENT STIFFNESS MATRIX FILE
OPEN(UNIT        = ELE_STIFF,       &
     ACCESS      = 'DIRECT',        &!!!!!!!!
     RECL        = 1024 * 3,        &
     ERR         = 50,              &
     BUFFERED    = 'YES',           &
     BLOCKSIZE   = 16384,           &!!!!!!!!
     BUFFERCOUNT = 4,               &
     FORM        = 'UNFORMATTED',   &
     DISPOSE     = 'DELETE',        &
     FILE        = 'ELE_STIFF')
               
! STIFFNESS (GLOBAL) FILE
OPEN(UNIT        = G_STIFF,         &
     ACCESS      = 'SEQUENTIAL',    &!!!!!!!!!
     ERR         = 60,              &
     BUFFERED    = 'YES',           &
     BLOCKSIZE   = 16384,           &
     BUFFERCOUNT = 4,               &
     FORM        = 'UNFORMATTED',   &
     DISPOSE     = 'DELETE',        &
     FILE        = 'G_STIFF')      
          
!-- INITIATE ELEMENT LIBARARY --
CALL INIT_ELE_LIB
               
!-- READ BASIC DATA --
COMMAND = GET_MACRO(CONTROL)
IF(INDEX(COMMAND, 'BASIC_DATA') .NE. 1)  CALL ERROR("'BASIC_DATA' should appear first")

! READ SOLUTION TYPES
COMMAND = REMOVE_FIRSTWORD(COMMAND) 
IF(LEN_TRIM(COMMAND) .EQ. 0) CALL ERROR('Solution type is missing!')!!!!!!!!!! tube.ctr ÷–solution√ª”–type

DO WHILE(LEN_TRIM(COMMAND) .GT. 0)
   READ(COMMAND,*) ITEM
   SELECT CASE(TRIM(ITEM))
      CASE ('MECHANICAL')
         SOLUTION_TYPE%MECHANICAL = .TRUE.
                 
      CASE DEFAULT
         CALL ERROR('Invalid solution type (' // TRIM(ITEM) //')!')
   END SELECT
              
   COMMAND = REMOVE_FIRSTWORD(COMMAND)
ENDDO

! BRANCH TO READ SUB-ITEMS
DO
   COMMAND = GET_MACRO(CONTROL)
     
   IF(TRIM(COMMAND) .EQ. 'END BASIC_DATA') EXIT
   
   READ(COMMAND,*) ITEM     
   SELECT CASE (TRIM(ITEM))   
      ! NODE COORDINATES
      CASE ('COORDINATES')
         READ(COMMAND,*) ITEM, NUM_NODE
         CALL READ_XYZ

      ! ELEMENT INFORMATION
      CASE ('ELEMENTS')
         READ(COMMAND,*) ITEM, NUM_ELEMENT
         CALL READ_ELEMENTS
      
      ! MATERIAL INFORMATION
      CASE ('MATERIALS')
         READ(COMMAND,*) ITEM, NUM_MATERIAL
         CALL READ_MATERIALS
         
      ! GEOMETRY INFORMATION
      CASE ('GEOMETRIES')
         READ(COMMAND,*) ITEM, NUM_GEOMETRY
         CALL READ_GEOMETRIES

      CASE DEFAULT
         CALL ERROR('Invalid command: ' // TRIM(ITEM) // ' in BASIC DATA!')
   END SELECT
ENDDO
   
!-- CHECK DATA --
CALL CHECK_DATA

!-- INITIATE VARIABLES USED IN SOLUTION PROCESS --
CALL INIT_SOLUTION 

RETURN

 10 CALL ERROR('Failed to open ' // TRIM(PROJECT_NAME) // '.LOG !')
 
 20 CALL ERROR('Failed to open ' // TRIM(PROJECT_NAME) // '.CTR !')

 30 CALL ERROR('Failed to open ' // TRIM(PROJECT_NAME) // '.BND !')

 40 CALL ERROR('Failed to open ' // TRIM(PROJECT_NAME) // '.OUT !')

 50 CALL ERROR('Failed to open element stiffness file!')

 60 CALL ERROR('Failed to open global stiffness file!')

END SUBROUTINE INITIATE