!***************************************************************************************************
!- PURPORSE:
!     CALCULATE THE SHAPE FUNCTIONS AND THEIR DERIVATIVES OVER NATURAL COORDINATES.
!  
!- INPUT ARGUMENTS:
!  ELEMENT_LIB: THE INFORMATION OF THIS ELEMENT TYPE
!
!- OUTPUT ARGUMENTS:
!  ELEMENT_LIB: THE INFORMATION OF THIS ELEMENT TYPE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : ERROR
!  EXTERNAL PROCEDURE: SHAPE_2D 
!
!- CALLED BY
!  INIT_ELE_LIB
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE INIT_SHAPE(ELEMENT_LIB)

USE SOLUTION_DATA, ONLY: INT_KIND, ELEMENT, ERROR
                          
IMPLICIT NONE

TYPE(ELEMENT), INTENT(INOUT):: ELEMENT_LIB   ! THE INFORMATION OF THIS ELEMENT TYPE
INTEGER(INT_KIND)              ERR           ! ERROR INDEX
INTEGER(INT_KIND)              NUM_NODE      ! THE NUMBER OF NODES IN AN ELEMENT
INTEGER(INT_KIND)              NUM_INTP      ! THE NUMBER OF INTEGRATION POINTS IN AN ELEMENT

!-- CALCULATE SHAPE FUNCTION AND ITS DERIVATIVES --
NUM_NODE = ELEMENT_LIB%NUM_NODE
NUM_INTP = ELEMENT_LIB%NUM_INTP

SELECT CASE(TRIM(ELEMENT_LIB%ELEMENT_TYPE))
   CASE ('TRIANGLE3', 'TRIANGLE6', 'RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9')    
      !-- ALLOCATE MEMORY FOR SHAPE FUNCTION --
      ALLOCATE(ELEMENT_LIB%SHAPE(NUM_NODE,NUM_INTP), STAT = ERR)
      IF(ERR .NE. 0) CALL ERROR('Fail to allocate memory for shape functions (PLANE ELEMENTS)!')

      ALLOCATE(ELEMENT_LIB%DSHAPE(2,NUM_NODE,NUM_INTP), STAT = ERR)
      IF(ERR .NE. 0) CALL ERROR('Fail to allocate memory for GAUSS_DSHAPE (PLANE ELEMENTS)!')
      
      CALL SHAPE_2D(ELEMENT_LIB%INTE_COORD, NUM_INTP, NUM_NODE, ELEMENT_LIB%SHAPE, ELEMENT_LIB%DSHAPE)
      
   CASE DEFAULT
      CALL ERROR("Invalid element type '" // TRIM(ELEMENT_LIB%ELEMENT_TYPE) // "'!")         
END SELECT

END SUBROUTINE INIT_SHAPE