!***************************************************************************************************
!- PURPORSE:
!     INITIATE INTEGRATION POINTS FOR NUMERICAL INTEGRATIONS.
!  
!- INPUT ARGUMENTS:
!  ELEMENT_LIB: THE INFORMATION OF THIS ELEMENT TYPE
!
!- OUTPUT ARGUMENTS:
!  ELEMENT_LIB: THE INFORMATION OF THIS ELEMENT TYPE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: ERROR
!
!- CALLED BY
!  INIT_ELE_LIB
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE INIT_INTEGRATION(ELEMENT_LIB)

USE BASIC_DATA, ONLY: INT_KIND, REAL_KIND, GAUSS_COORD2, GAUSS_COORD3, GAUSS_WEIGHT1, GAUSS_WEIGHT2, GAUSS_WEIGHT3, ELEMENT, ERROR
                          
IMPLICIT NONE

TYPE(ELEMENT), INTENT(INOUT):: ELEMENT_LIB   ! THE INFORMATION OF THIS ELEMENT TYPE
INTEGER(INT_KIND)              I             ! LOOP INDEX
INTEGER(INT_KIND)              J             ! LOOP INDEX
INTEGER(INT_KIND)              N_GAUSS       ! THE INDEX FOR Nth GAUSS POINT
INTEGER(INT_KIND)              ERR           ! ERROR INDEX

!-- ALLOCATE MEMORY FOR INTEGRATION POINT --
ALLOCATE(ELEMENT_LIB%INTE_COORD(ELEMENT_LIB%NUM_INTP), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate memory for integration points (INIT_INTP)!')

!-- SET INFORMAION OF INTEGRATION POINT --
SELECT CASE(TRIM(ELEMENT_LIB%ELEMENT_TYPE))
   CASE ('TRIANGLE3')    ! THE 2ND ORDER HAMMER INTEGRATION 王勖成《有限单元法》2003年版 P150
      ELEMENT_LIB%INTE_COORD(1)%COORD(1) = 1.0_REAL_KIND / 3.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(1)%COORD(2) = 1.0_REAL_KIND / 3.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(1)%COORD(3) = 1.0_REAL_KIND / 3.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(1)%WEIGHT   = 1.0_REAL_KIND / 2.0_REAL_KIND ! |J| = 2 * AEREA
      
   CASE ('TRIANGLE6')    ! THE 2ND ORDER HAMMER INTEGRATION 王勖成《有限单元法》2003年版 P150
      ELEMENT_LIB%INTE_COORD(1)%COORD(1) = 2.0_REAL_KIND / 3.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(1)%COORD(2) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(1)%COORD(3) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(2)%COORD(1) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(2)%COORD(2) = 2.0_REAL_KIND / 3.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(2)%COORD(3) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(3)%COORD(1) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(3)%COORD(2) = 1.0_REAL_KIND / 6.0_REAL_KIND
      ELEMENT_LIB%INTE_COORD(3)%COORD(3) = 2.0_REAL_KIND / 3.0_REAL_KIND        
      ELEMENT_LIB%INTE_COORD(:)%WEIGHT   = 1.0_REAL_KIND / 3.0_REAL_KIND / 2.0_REAL_KIND   ! |J| = 2 * AEREA

   CASE ('RECTANGLE4')
      N_GAUSS = 0
      DO I = 1, 2          ! KESI
         DO J = 1, 2       ! ETA
            N_GAUSS = N_GAUSS + 1
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(1) = GAUSS_COORD2(I)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(2) = GAUSS_COORD2(J)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%WEIGHT   = GAUSS_WEIGHT2(I) * GAUSS_WEIGHT2(J)
         ENDDO
      ENDDO

   CASE ('RECTANGLE8')
      N_GAUSS = 0
      DO I = 1, 3          ! KESI
         DO J = 1, 3       ! ETA
            N_GAUSS = N_GAUSS + 1
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(1) = GAUSS_COORD3(I)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(2) = GAUSS_COORD3(J)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%WEIGHT   = GAUSS_WEIGHT3(I) * GAUSS_WEIGHT3(J)
         ENDDO
      ENDDO
      
   CASE ('RECTANGLE9')
      N_GAUSS = 0
      DO I = 1, 3          ! KESI
         DO J = 1, 3       ! ETA
            N_GAUSS = N_GAUSS + 1
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(1) = GAUSS_COORD3(I)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%COORD(2) = GAUSS_COORD3(J)
            ELEMENT_LIB%INTE_COORD(N_GAUSS)%WEIGHT   = GAUSS_WEIGHT3(I) * GAUSS_WEIGHT3(J)
         ENDDO
      ENDDO 
      
   CASE DEFAULT
      CALL ERROR("Invalid element type '" // TRIM(ELEMENT_LIB%ELEMENT_TYPE) // "'!")
END SELECT

END SUBROUTINE INIT_INTEGRATION