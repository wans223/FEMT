!***************************************************************************************************
!- PURPORSE:
!     INITIATE VARIABLES FOR MECHANICAL ANALYSIS.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: ERROR
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE INIT_MECHANICAL

USE SOLUTION_DATA, ONLY: INT_KIND, NUM_NODE, NUM_ELEMENT, ELEMENTS, NUM_DISP_EQUATION, NODE_DISP_DOF, &
                         DISPLACEMENT, NODAL_STRAIN, NODAL_STRESS, INTE_STRAIN, INTE_STRESS, &
                         FORCE, ELE_FORCE, ERROR, ELEMENT, ELEMENT_LIB

IMPLICIT NONE

INTEGER(INT_KIND)        ERR          ! ERR INDEX
INTEGER(INT_KIND)        I            ! LOOP INDEX
INTEGER(INT_KIND)        J            ! LOOP INDEX
INTEGER(INT_KIND)        NUM_STRAIN   ! THE NUMBER OF STRAINS AT A POINT
TYPE(ELEMENT), POINTER:: P_ELE_TYPE   ! A POINTER TO ELEMENT TYPE

!-- ASSIGN EQUATION NUMBER FOR EACH NODE --
ALLOCATE(NODE_DISP_DOF(NUM_NODE), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODE_DISP_DOF!')

NUM_DISP_EQUATION = 0
DO I = 1, NUM_NODE
   ! SET THE NUMBER OF DOFS FOR EACH NODE
   NODE_DISP_DOF(I)%NUM_DOF = 0
   DO J = 1, NUM_ELEMENT
      ! THE SAME NODE MAY HAVE DIFFERENT NUMBER OF DOFS IN DIFFERENT KIND OF ELEMENTS
      P_ELE_TYPE => ELEMENT_LIB(ELEMENTS(J)%ELEMENT)
      IF(ANY(ELEMENTS(J)%NODE .EQ. I) .AND. & ! SET NODE THE MAXIMUM DOF OF NEARBY ELEMENTS
         NODE_DISP_DOF(I)%NUM_DOF .LT. P_ELE_TYPE%DISP_DOF) NODE_DISP_DOF(I)%NUM_DOF = P_ELE_TYPE%DISP_DOF
   ENDDO
   
   ALLOCATE(NODE_DISP_DOF(I)%DOFS(NODE_DISP_DOF(I)%NUM_DOF), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODE_DISP_DOF%DOFS!')
   
   DO J = 1, NODE_DISP_DOF(I)%NUM_DOF
      NUM_DISP_EQUATION        = NUM_DISP_EQUATION + 1
      NODE_DISP_DOF(I)%DOFS(J) = NUM_DISP_EQUATION
   ENDDO
ENDDO

!-- ALLOCATE WORKING ARRAYS --
ALLOCATE(DISPLACEMENT(NUM_DISP_EQUATION), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate DISPLACEMENT!')
DISPLACEMENT = 0.0

ALLOCATE(NODAL_STRAIN(NUM_NODE), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODAL_STRAIN!')
ALLOCATE(NODAL_STRESS(NUM_NODE), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODAL_STRESS!')
DO I = 1, NUM_NODE
   ALLOCATE(NODAL_STRAIN(I)%VALUE(3), STAT = ERR) ! Ex, Ey, Exy   
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate values of NODAL_STRAIN!')
   NODAL_STRAIN(I)%VALUE = 0.0
   
   ALLOCATE(NODAL_STRESS(I)%VALUE(3), STAT = ERR) ! Sx, Sy, Sxy   
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate values of NODAL_STRESS!')
   NODAL_STRESS(I)%VALUE = 0.0
ENDDO

ALLOCATE(INTE_STRAIN(NUM_ELEMENT), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate INTE_STRAIN!')

ALLOCATE(INTE_STRESS(NUM_ELEMENT), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate INTE_STRESS!')

ALLOCATE(ELE_FORCE(NUM_ELEMENT), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate ELEMENT_FORCE!')

ALLOCATE(FORCE(NUM_DISP_EQUATION), STAT = ERR)
IF(ERR .NE. 0) CALL ERROR('Fail to allocate FORCE!')
FORCE = 0.0

!-- ALLOCATE MEMORIES FOR ELEMENTAL VARIABLES --
DO I = 1, NUM_ELEMENT
   P_ELE_TYPE => ELEMENT_LIB(ELEMENTS(I)%ELEMENT)

   SELECT CASE(TRIM(P_ELE_TYPE%ELEMENT_TYPE))
      CASE ('TRIANGLE3', 'TRIANGLE6', 'RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9') ! 2D ELEMENTS
         NUM_STRAIN = 3 
         
      CASE DEFAULT
   END SELECT
   
   ALLOCATE(INTE_STRAIN(I)%VALUE(NUM_STRAIN * P_ELE_TYPE%NUM_INTP), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate INTE_STRAIN%VALUE!')
   INTE_STRAIN(I)%VALUE = 0.0
   
   ALLOCATE(INTE_STRESS(I)%VALUE(NUM_STRAIN * P_ELE_TYPE%NUM_INTP), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate INTE_STRESS%VALUE!')
   INTE_STRESS(I)%VALUE = 0.0
   
   ALLOCATE(ELE_FORCE(I)%VALUE(P_ELE_TYPE%NUM_NODE * P_ELE_TYPE%DISP_DOF), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Fail to allocate ELEMENT_FORCE%VALUE!')
   ELE_FORCE(I)%VALUE = 0.0   
ENDDO

END SUBROUTINE INIT_MECHANICAL