!***************************************************************************************************
!- PURPORSE:
!     READ GIVEN NODAL FORCES.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: GET_MACRO, REMOVE_FIRSTWORD, ERROR
!
!- CALLED BY
!  BOUNDARY_FORCE
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE READ_NODAL_FORCE

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, WORD_KIND, LINE_KIND, REAL_EPS, NUM_NODE, BOUNDARY, &
                         GET_MACRO, REMOVE_FIRSTWORD, ERROR, NUM_NODAL_FORCE, NODE_DISP_DOF, NODAL_FORCE

IMPLICIT NONE

CHARACTER(LINE_KIND) COMMAND
CHARACTER(WORD_KIND) ITEM       ! A TEMPERARY VARIABLE TO STORE STRINGS
INTEGER(INT_KIND)::  FIRST = 0
INTEGER(INT_KIND)::  LAST  = 0
INTEGER(INT_KIND)    I          ! LOOP INDEX
INTEGER(INT_KIND)    J          ! LOOP INDEX
INTEGER(INT_KIND)    K          ! LOOP INDEX
INTEGER(INT_KIND)    SUM        ! THE TOTAL NUMBER OF FORCE BOUNDARY CONDITION
INTEGER(INT_KIND)    ERR        ! ERR INDEX
REAL(REAL_KIND)      VALUE      ! THE VALUE OF BOUNDARY FORCE

!-- READ GIVEN NODAL FORCE
SUM = 0
K   = 0
COMMAND = GET_MACRO(BOUNDARY)
DO WHILE(TRIM(COMMAND) .NE. 'END NODAL_FORCE')     
   IF(INDEX(COMMAND, 'TO') .NE. 0) THEN
      READ(COMMAND,*) FIRST, ITEM, LAST
      IF(LAST .LT. FIRST) CALL ERROR('Last node No. < first node No. ! (NODAL_FORCE)')
      COMMAND = REMOVE_FIRSTWORD(COMMAND) ! REMOVE FIRST NUMBER
      COMMAND = REMOVE_FIRSTWORD(COMMAND) ! REMOVE 'TO'
   ELSE
      READ(COMMAND,*) FIRST
      LAST    = FIRST
   ENDIF
   IF(FIRST .LE. 0 .OR. LAST .GT. NUM_NODE) CALL ERROR('Invalid node in NODAL_FORCE! (' // TRIM(COMMAND) // ' )')
   COMMAND = REMOVE_FIRSTWORD(COMMAND)    ! REMOVE LAST NUMBER
                
   K   = SUM + 1
   SUM = SUM + LAST - FIRST + 1       
   DO WHILE(LEN_TRIM(COMMAND) .GT. 0)
      READ(COMMAND,*) ITEM, VALUE
      COMMAND = REMOVE_FIRSTWORD(COMMAND)
      COMMAND = REMOVE_FIRSTWORD(COMMAND)
       
      SELECT CASE(TRIM(ITEM))
         CASE ('F_X')
            J = 1
         CASE ('F_Y')
            J = 2
         CASE DEFAULT
            CALL ERROR("Invalid nordal force type '" // TRIM(ITEM) // "'!")
      END SELECT
       
      DO I = K, SUM
         IF(.NOT. ALLOCATED(NODAL_FORCE(I)%GIVEN)) THEN
            NODAL_FORCE(I)%NODE = FIRST + I - K
            ALLOCATE(NODAL_FORCE(I)%GIVEN(NODE_DISP_DOF(FIRST + I - K)%NUM_DOF), STAT = ERR)
            IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODAL_FORCE%GIVEN!')
            NODAL_FORCE(I)%GIVEN = .FALSE.
            ALLOCATE(NODAL_FORCE(I)%VALUE(NODE_DISP_DOF(FIRST + I - K)%NUM_DOF), STAT = ERR)
            IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODAL_FORCE%VALUE!')
            NODAL_FORCE(I)%VALUE = 0.0
         ENDIF
         NODAL_FORCE(I)%GIVEN(J) = .TRUE.
         NODAL_FORCE(I)%VALUE(J) = VALUE
      ENDDO
   ENDDO
    
   COMMAND = GET_MACRO(BOUNDARY)
ENDDO

IF(SUM .NE. NUM_NODAL_FORCE) CALL ERROR('NUM_NODAL_FORCE is incorrect!')

END SUBROUTINE READ_NODAL_FORCE