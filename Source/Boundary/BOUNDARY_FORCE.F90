!***************************************************************************************************
!- PURPORSE:
!     ENTRY POINT OF READING FORCE BOUNDARY CONDITION.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE:   GET_MACRO, ERROR
!  EXTERNAL PROCEDURE: READ_NODAL_FORCE, READ_LINE_FORCE
!
!- CALLED BY
!  BOUNDARY_CONDITION
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE BOUNDARY_FORCE

USE SOLUTION_DATA, ONLY: INT_KIND, WORD_KIND, LINE_KIND, BOUNDARY, GET_MACRO, ERROR, NUM_NODAL_FORCE, &
                         NUM_LINE_FORCE, NODAL_FORCE, LINE_FORCE

IMPLICIT NONE

CHARACTER(LINE_KIND) COMMAND
CHARACTER(WORD_KIND) ITEM    ! A TEMPERARY VARIABLE TO STORE STRINGS
INTEGER(INT_KIND)    NUMBER  ! A TEMPERARY VARIABLE TO STORE INTEGER NUMBERS
INTEGER(INT_KIND)    ERR     ! ERROR INDEX

!-- READ FORCE BONDARY CONDITION
DO
   COMMAND = GET_MACRO(BOUNDARY)
   
   IF(TRIM(COMMAND) .EQ. 'END FORCE') EXIT
   
   READ(COMMAND,*) ITEM, NUMBER
   IF(NUMBER .LE. 0) CALL ERROR('The number of load items should be greater than 0!')
   
   ! FOR DIFFERENT KIND OF FORCE BOUNDARY CONDITION
   SELECT CASE(TRIM(ITEM))
      CASE ('NODAL_FORCE')
         NUM_NODAL_FORCE = NUMBER
         ALLOCATE(NODAL_FORCE(NUMBER), STAT = ERR)
         IF(ERR .NE. 0) CALL ERROR('Fail to allocate NODAL_FORCE!')
         
         CALL READ_NODAL_FORCE
           
      CASE ('LINE_FORCE')
         NUM_LINE_FORCE = NUMBER
         ALLOCATE(LINE_FORCE(NUMBER), STAT = ERR)
         IF(ERR .NE. 0) CALL ERROR('Fail to allocate LINE_FORCE!')
         
         CALL READ_LINE_FORCE
                  
      CASE DEFAULT
         CALL ERROR('Invalid force type: ' // TRIM(COMMAND))
   END SELECT
ENDDO

END SUBROUTINE BOUNDARY_FORCE