!***************************************************************************************************
!- PURPORSE:
!     READ GIVEN LINE FORCES.
!  
!- INPUT ARGUMENTS:
!  NONE
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE: GET_MACRO, REMOVE_FIRSTWORD, ERROR
!
!- CALLED BY
!  BOUNDARY_FORCE
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 17, 2016
!***************************************************************************************************

SUBROUTINE READ_LINE_FORCE

USE SOLUTION_DATA, ONLY: INT_KIND, LINE_KIND, WORD_KIND, BOUNDARY, NUM_LINE_FORCE, LINE_FORCE, ELEMENTS, &
                         ELEMENT_LIB, GET_MACRO, REMOVE_FIRSTWORD, ERROR

IMPLICIT NONE

CHARACTER(LINE_KIND) COMMAND
CHARACTER(WORD_KIND) ITEM       ! A TEMPERARY VARIABLE TO STORE STRINGS
CHARACTER(WORD_KIND) ELE_TYPE   ! ELEMENT TYPE
INTEGER(INT_KIND)    I          ! THE COUNTER OF LINE FORCE BOUNDARY CONDITION
INTEGER(INT_KIND)    NUM_NODE   ! THE NUMBER OF NODES APPLIED TRANCTION
INTEGER(INT_KIND)    J          ! LOOP INDEX

!-- READ GIVEN LINE FORCE
I = 0

DO
   COMMAND = GET_MACRO(BOUNDARY)
   IF(TRIM(COMMAND) .EQ. 'END LINE_FORCE') EXIT                
   I = I + 1
   
   ! READ ELEMENT NUMBER
   READ(COMMAND,*) LINE_FORCE(I)%ELEMENT
   COMMAND = REMOVE_FIRSTWORD(COMMAND)    ! REMOVE ELEMENT NUMBER
   COMMAND = REMOVE_FIRSTWORD(COMMAND)    ! REMOVE 'SIDE'
   
   ! READ NODES
   ELE_TYPE = ELEMENT_LIB(ELEMENTS(LINE_FORCE(I)%ELEMENT)%ELEMENT)%ELEMENT_TYPE
   SELECT CASE(TRIM(ELE_TYPE))
      CASE ('TRIANGLE3', 'RECTANGLE4')
         NUM_NODE = 2
                      
      CASE ('TRIANGLE6', 'RECTANGLE8', 'RECTANGLE9')
         NUM_NODE = 3     
         
      CASE DEFAULT
         CALL ERROR("Invalid element type '" // TRIM(ELE_TYPE) // "' for applying line force!")
   END SELECT 
   
   READ(COMMAND,*) LINE_FORCE(I)%NODE(1:NUM_NODE), ITEM
   DO J = 1, NUM_NODE + 1
      COMMAND = REMOVE_FIRSTWORD(COMMAND)
   ENDDO
   
   ! READ TRACTIONS
   LINE_FORCE(I)%TRACTION = 0.0
   
   DO WHILE(LEN_TRIM(COMMAND) .GT. 0)
      SELECT CASE(TRIM(ITEM))
         CASE ('F_N')   ! NORMAL TRACTION, PRESSURE IS POSITIVE
            READ(COMMAND,*) LINE_FORCE(I)%TRACTION(1,1:NUM_NODE)
            
         CASE ('F_T')   ! TANGENT TRACTION
            READ(COMMAND,*) LINE_FORCE(I)%TRACTION(2,1:NUM_NODE)
                     
         CASE DEFAULT
            CALL ERROR("Invalid line force type '" // TRIM(ITEM) // "'!")
      END SELECT
      
      DO J = 1, NUM_NODE
         COMMAND = REMOVE_FIRSTWORD(COMMAND)
      ENDDO
   ENDDO
ENDDO

IF(I .NE. NUM_LINE_FORCE) CALL ERROR('NUM_LINE_FORCE is incorrect!')

END SUBROUTINE READ_LINE_FORCE