!***************************************************************************************************
!- PURPORSE:
!     CALCULATE THE ELEMENT STIFFNESS MATRIX OF 3, 4, 6, 8 NODE PLANE ELEMENT.
!  
!- INPUT ARGUMENTS:
!  I_ELEMENT: THE ELEMENT NUMBER
!  NUM_NODE : THE NUMBER OF ELEMENT NODES
!  OPTION   : PROBLEM TYPE(PLANE STRESS OR PLANE STRAIN)
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : NUM2STR, ERROR
!  EXTERNAL PROCEDURE: MATERIAL_2D 
!
!- CALLED BY
!  STIFFNESS
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE STIFFNESS_PLANE(I_ELEMENT, NUM_NODE, OPTION)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, WORD_KIND, REAL_EPS, NUM_OPT, ELE_STIFF, XYZ, ELEMENTS, &
                         MATERIALS, ERROR, NUM2STR, ELEMENT, ELEMENT_LIB, GEOMETRIES
                          
IMPLICIT NONE

INTEGER(INT_KIND), INTENT(IN)::    I_ELEMENT                              ! THE ELEMENT NUMBER
INTEGER(INT_KIND), INTENT(IN)::    NUM_NODE                               ! THE NUMBER OF ELEMENT NODES
CHARACTER(WORD_KIND), INTENT(IN):: OPTION(NUM_OPT)                        ! PLANE STRESS OR PLANE STRAIN
INTEGER(INT_KIND)                  I                                      ! LOOP INDEX
INTEGER(INT_KIND)                  J                                      ! LOOP INDEX
INTEGER(INT_KIND)                  K                                      ! LOOP INDEX
TYPE(ELEMENT), POINTER::           P_ELE_TYPE                             ! A POINTER TO ELEMENT TYPE
REAL(REAL_KIND)                    THICKNESS                              ! ELEMENT THICKNESS
REAL(REAL_KIND)                    ELE_XY(2,NUM_NODE)                     ! X AND Y COORDINATES OF ELEMENT NODES
REAL(REAL_KIND)                    D(3,3)                                 ! MATERIAL MATRIX
REAL(REAL_KIND)                    JACOBI(2,2)                            ! JACOBIAN MATRIX J
REAL(REAL_KIND)                    INV_J(2,2)                             ! THE INVERSE OF JACOBIAN MATRIX J
REAL(REAL_KIND)                    DETERMINT                              ! |J|
REAL(REAL_KIND)                    D_SHAPE_XY(2,NUM_NODE)                 ! THE SHAPE FUNCTION OVER COORDINATES
REAL(REAL_KIND)                    B(3,NUM_NODE * 2)                      ! STRAIN MATRIX
REAL(REAL_KIND)                    KE(NUM_NODE * 2,NUM_NODE * 2)          ! FULL ELEMENT STIFFNESS MATRIX
REAL(REAL_KIND)                    UPPER_K(NUM_NODE * (NUM_NODE * 2 + 1)) ! THE UPPER PART OF STIFFNESS MATRIX

!-- REFERENCE THE ELEMENT TYPE --
P_ELE_TYPE => ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)

!-- GET THE COORDINATE OF ELEMENT NODES --
DO I = 1, NUM_NODE
   ELE_XY(1:2,I) = XYZ(1:2,ELEMENTS(I_ELEMENT)%NODE(I))
ENDDO 

!-- SET MATERIAL MATRIX --
CALL MATERIAL_2D(MATERIALS(ELEMENTS(I_ELEMENT)%MATERIAL), OPTION(1), D)

!-- CALCULATE ELEMENT STIFFNESS MATRIX --
THICKNESS = GEOMETRIES(ELEMENTS(I_ELEMENT)%GEOMETRY)%THICKNESS
KE        = 0.0

! INTEGRATE OVER ALL INTEGRATION POINTS
DO I = 1, P_ELE_TYPE%NUM_INTP
   ! CALCULATE JACOBIAN MATRIX
   JACOBI = MATMUL(P_ELE_TYPE%DSHAPE(:,:,I), TRANSPOSE(ELE_XY))
   
   ! CALCULATE THE DETERMINENT OF THE JACOBIAN MATRIX
   DETERMINT = JACOBI(1,1) * JACOBI(2,2) - JACOBI(1,2) * JACOBI(2,1)
   IF(ABS(DETERMINT) .LT. REAL_EPS) CALL ERROR('DETERMINT = ' // NUM2STR(DETERMINT) // ' IN ELEMENT ' // NUM2STR(I_ELEMENT))   
      
   ! CALCULATE THE SHAPE FUNCTION OVER COORDINATES (dN/dX = J^-1 * D_SHAPE)
   INV_J(1,1) =  JACOBI(2,2) / DETERMINT
   INV_J(2,2) =  JACOBI(1,1) / DETERMINT
   INV_J(1,2) = -JACOBI(1,2) / DETERMINT
   INV_J(2,1) = -JACOBI(2,1) / DETERMINT
   D_SHAPE_XY = MATMUL(INV_J, P_ELE_TYPE%DSHAPE(:,:,I))
   
   ! CALCULATE THE STRAIN MATRIX  
   B = 0.0
   DO J = 1, NUM_NODE
      B(1,J * 2 - 1) = D_SHAPE_XY(1,J)
      B(2,J * 2    ) = D_SHAPE_XY(2,J)
      B(3,J * 2 - 1) = D_SHAPE_XY(2,J)       
      B(3,J * 2    ) = D_SHAPE_XY(1,J)
   ENDDO
      
   ! CALCULATE ELEMENT STIFFNESS MATRIX
   KE = KE + MATMUL(MATMUL(TRANSPOSE(B), D), B) * THICKNESS * DETERMINT * P_ELE_TYPE%INTE_COORD(I)%WEIGHT
    
ENDDO   
   
!-- WRITE ELEMENT STIFFNESS TO DISK --
K = 0
DO I = 1, NUM_NODE * 2
   DO J = I, NUM_NODE * 2
      K          = K + 1
      UPPER_K(K) = KE(I,J)
   END DO
END DO
WRITE(ELE_STIFF, REC = I_ELEMENT) UPPER_K

END SUBROUTINE STIFFNESS_PLANE