!***************************************************************************************************
!- PURPORSE:
!     CALCULATE THE MATERIAL MATRIX OF 2D PLANE ELEMENTS.
!  
!- INPUT ARGUMENTS:
!  MATERIAL_INFO: MATERIAL INFORMATION
!  OPTION       : PLAIN STRESS OR PLAIN STRAIN
!
!- OUTPUT ARGUMENTS:
!  D: MATERIAL MATRIX
!
!- CALL PROCEDURES:
!  NONE
!
!- CALLED BY
!  STIFFNESS_PLANE, STRESS_GAUSS_PLANE, STRESS_NODAL_TRIANGLE, STRESS_NODAL_RECTANGLE
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, NOVEMBER 1, 2015
!***************************************************************************************************

SUBROUTINE MATERIAL_2D(MATERIAL_INFO, OPTION, D)

USE BASIC_DATA, ONLY: REAL_KIND, WORD_KIND, MATERIAL
                          
IMPLICIT NONE

TYPE(MATERIAL), INTENT(IN)::       MATERIAL_INFO   ! MATERIAL INFORMATION
CHARACTER(WORD_KIND), INTENT(IN):: OPTION          ! PLAIN STRESS OR PLAIN STRAIN  
REAL(REAL_KIND), INTENT(OUT)::     D(3,3)          ! MATERIAL MATRIX
   
! ISOTROPIC MATERIAL PROPERTIES
REAL(REAL_KIND)                    E               ! YOUNG'S MODULUS
REAL(REAL_KIND)                    V               ! POISSON'S RATIO

!-- GET MATERIAL PROPERTIES AND CREATE MATERIAL MATRIX --
D = 0.0
IF(MATERIAL_INFO%MATERIAL_TYPE .EQ. 'ISOTROPIC') THEN
   ! GET MATERIAL PROPERTIES
   E = MATERIAL_INFO%E
   V = MATERIAL_INFO%V

   IF(TRIM(OPTION) .EQ. 'PLANE_STRAIN') THEN
      E = E / (1.0 - V * V)
      V = V / (1.0 - V)
   ENDIF

   ! SET MATERIAL MATRIX
   D(1,1) = 1.0
   D(2,2) = 1.0
   D(3,3) = (1.0 - V) / 2.0
   D(1,2) = V
   D(2,1) = V
   D      = D * E / (1.0 - V * V)
ENDIF

END SUBROUTINE MATERIAL_2D