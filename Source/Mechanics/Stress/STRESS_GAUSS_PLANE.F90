!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRESS AT GAUSS POINTS FOR 3, 4, 6, 8 NODE PLANE ELEMENTS.
!  
!- INPUT ARGUMENTS:
!  I_ELEMENT: THE ELEMENT NUMBER
!  NUM_INTP : THE NUMBER OF INTEGRATION POINTS IN THIS ELEMENT
!  TP       : PLANE_STRAIN OR PLANE_STRESS
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  NONE
!
!- CALLED BY
!  GET_STRESS
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE STRESS_GAUSS_PLANE(I_ELEMENT, NUM_INTP, TP)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, WORD_KIND, ELEMENTS, MATERIALS, INTE_STRAIN, INTE_STRESS
                          
IMPLICIT NONE

INTEGER(INT_KIND), INTENT(IN)::    I_ELEMENT ! THE ELEMENT NUMBER
INTEGER(INT_KIND), INTENT(IN)::    NUM_INTP  ! THE NUMBER OF ELEMENT NODES
CHARACTER(WORD_KIND), INTENT(IN):: TP        ! PLANE_STRAIN OR PLANE_STRESS
INTEGER(INT_KIND)                  I         ! LOOP INDEX
REAL(REAL_KIND)                    D(3,3)    ! MATERIAL MATRIX

!-- SET MATERIAL MATRIX --
CALL MATERIAL_2D(MATERIALS(ELEMENTS(I_ELEMENT)%MATERIAL), TP, D)   

!-- CALCULATE STRESS AT INTEGRATION POINTS --
DO I = 1, NUM_INTP
   INTE_STRESS(I_ELEMENT)%VALUE((I - 1) * 3 + 1:I * 3) = MATMUL(D, INTE_STRAIN(I_ELEMENT)%VALUE((I - 1) * 3 + 1:I * 3))
ENDDO

END SUBROUTINE STRESS_GAUSS_PLANE