!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRESSES AT INTEGRATION POINTS OR NODES.
!  
!- INPUT ARGUMENTS:
!  OPTION: OPTION(1): 'GAUSS' OR 'NODAL' STRESS
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : ERROR
!  EXTERNAL PROCEDURE: STRAIN_GAUSS_PLANE, STRAIN_NODAL_PLANE
!
!- CALLED BY
!  SOLVER_MANAGER
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE GET_STRESS(OPTION)

USE SOLUTION_DATA, ONLY: INT_KIND, WORD_KIND, NUM_NODE, NUM_ELEMENT, NUM_OPT, ELEMENT_LIB, &
                         ELEMENTS, NODAL_STRESS, ERROR

IMPLICIT NONE

!-- DATA_DICTIONARY
CHARACTER(WORD_KIND), INTENT(IN):: OPTION(NUM_OPT)  ! OPTION(1): 'GAUSS' OR 'NODAL' STRAIN; OPTION(2): PLANE_STRAIN OR PLANE_STRESS
INTEGER(INT_KIND)                  I_ELEMENT        ! LOOP INDEX
INTEGER(INT_KIND)                  I_NODE           ! LOOP INDEX
INTEGER(INT_KIND)                  ERR              ! ERROR INDEX
INTEGER(INT_KIND), ALLOCATABLE::   ADJ_ELE(:)       ! THE TOTAL NUMBER OF ELEMENTS AROUND A NODE

!-- DATA_PARAMETER
!直接定义WORD_KIND的‘GAUSS’
CHARACTER(WORD_KIND),PARAMETER:: TEMP = 'GAUSS'

!-- CALCULATE STRAINS AT GAUSS POINTS --
!CALL GET_STRAIN(TRANSFER('GAUSS', OPTION(1), WORD_KIND))
CALL GET_STRAIN(TEMP)

!-- CALCULATE STRESSES AT GAUSS POINTS IN EACH ELEMENT --
DO I_ELEMENT = 1, NUM_ELEMENT
   SELECT CASE(TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE))
      CASE ('TRIANGLE3', 'TRIANGLE6', 'RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9')
         CALL STRESS_GAUSS_PLANE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_INTP, OPTION(2))   

      CASE DEFAULT
         CALL ERROR('Invalid element type (' // TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE) // ') in GET_STRESS!')
   END SELECT
ENDDO

!-- CALCULATE STRESSES AT NODES --
IF(TRIM(OPTION(1)) .EQ. 'NODAL') THEN
   ! INITIATE DATA   
   ALLOCATE(ADJ_ELE(NUM_NODE), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Failed to allocate ADJ_ELE!')
   ADJ_ELE = 0 
   
   ! CALCULATE ELEMENT NODAL STRESS
   DO I_ELEMENT = 1, NUM_ELEMENT
      SELECT CASE(TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE))
         CASE ('TRIANGLE3', 'TRIANGLE6')
            CALL STRESS_NODAL_TRIANGLE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_NODE, ADJ_ELE, OPTION(2))
         
         CASE ('RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9')
            CALL STRESS_NODAL_RECTANGLE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_NODE, ADJ_ELE, OPTION(2))

      END SELECT
   ENDDO

   ! AVERAGE THE NODAL STRAIN OVER ADJECENT ELEMENTS
   DO I_NODE = 1, NUM_NODE
      NODAL_STRESS(I_NODE)%VALUE = NODAL_STRESS(I_NODE)%VALUE / ADJ_ELE(I_NODE)
   ENDDO
   
   DEALLOCATE(ADJ_ELE)
ENDIF

END SUBROUTINE GET_STRESS