!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRAINS AT GAUSS POINTS FOR 3, 4, 6, 8 NODE PLANE ELEMENTS.
!  
!- INPUT ARGUMENTS:
!  I_ELEMENT: THE ELEMENT NUMBER
!  NUM_NODE : THE NUMBER OF ELEMENT NODES
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  NONE
!
!- CALLED BY
!  GET_STRAIN
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE STRAIN_GAUSS_PLANE(I_ELEMENT, NUM_NODE)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, XYZ, ELEMENTS, ELEMENT, ELEMENT_LIB, DISPLACEMENT, &
                         NODE_DISP_DOF, INTE_STRAIN
                          
IMPLICIT NONE

INTEGER(INT_KIND), INTENT(IN):: I_ELEMENT              ! THE ELEMENT NUMBER
INTEGER(INT_KIND), INTENT(IN):: NUM_NODE               ! THE NUMBER OF ELEMENT NODES
INTEGER(INT_KIND)               I, J                   ! LOOP INDEX
TYPE(ELEMENT), POINTER::        P_ELE_TYPE             ! A POINTER TO ELEMENT TYPE
REAL(REAL_KIND)                 ELE_XY(2,NUM_NODE)     ! X AND Y COORDINATES OF ELEMENT NODES
REAL(REAL_KIND)                 JACOBI(2,2)            ! JACOBIAN MATRIX J
REAL(REAL_KIND)                 INV_J(2,2)             ! THE INVERSE OF JACOBIAN MATRIX J
REAL(REAL_KIND)                 DETERMINT              ! |J|
REAL(REAL_KIND)                 D_SHAPE_XY(2,NUM_NODE) ! THE SHAPE FUNCTION OVER COORDINATES
REAL(REAL_KIND)                 B(3,NUM_NODE * 2)      ! STRAIN MATRIX
REAL(REAL_KIND)                 ELE_DIS(NUM_NODE * 2)  ! ELEMENT NODAL DISPLACEMENTS

!-- REFERENCE THE ELEMENT TYPE --
P_ELE_TYPE => ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)

!-- GET THE COORDINATE OF ELEMENT NODES --
DO I = 1, NUM_NODE
   ELE_XY(1:2,I) = XYZ(1:2,ELEMENTS(I_ELEMENT)%NODE(I))
ENDDO

!-- GET THE DISPLACEMENT OF ELEMENT NODES --
DO I = 1, NUM_NODE
   DO J = 1, 2
      ELE_DIS((I - 1) * 2 + J) = DISPLACEMENT(NODE_DISP_DOF(ELEMENTS(I_ELEMENT)%NODE(I))%DOFS(J))
   ENDDO
ENDDO 

!-- CALCULATE ELEMENT STRAINS AT EACH INTEGRATION POINT --
DO I = 1, P_ELE_TYPE%NUM_INTP
   ! CALUCATE JACOBIAN MATRIX
   JACOBI = MATMUL(P_ELE_TYPE%DSHAPE(:,:,I), TRANSPOSE(ELE_XY))

   ! CALCULATE THE DETERMINENT OF THE JACOBIAN MATRIX
   DETERMINT = JACOBI(1,1) * JACOBI(2,2) - JACOBI(1,2) * JACOBI(2,1)
      
   ! CALCULATE THE SHAPE FUNCTION OVER COORDINATES (dN/dX = J^-1 * D_SHAPE)
   INV_J(1,1) =  JACOBI(2,2) / DETERMINT
   INV_J(2,2) =  JACOBI(1,1) / DETERMINT
   INV_J(1,2) = -JACOBI(1,2) / DETERMINT
   INV_J(2,1) = -JACOBI(2,1) / DETERMINT
   D_SHAPE_XY = MATMUL(INV_J, P_ELE_TYPE%DSHAPE(:,:,I))
       
   ! CALCULATE THE STRAIN MATRIX  
   B = 0.0
   DO J = 1, NUM_NODE
      B(1,J * 2 - 1) = D_SHAPE_XY(1,J)
      B(2,J * 2    ) = D_SHAPE_XY(2,J)
      B(3,J * 2 - 1) = D_SHAPE_XY(2,J)       
      B(3,J * 2    ) = D_SHAPE_XY(1,J)
   ENDDO
      
   ! CALCULATE STRAINS AT THIS INTEGRATION POINT
   INTE_STRAIN(I_ELEMENT)%VALUE((I - 1) * 3 + 1:I * 3) =  MATMUL(B, ELE_DIS)
ENDDO   

END SUBROUTINE STRAIN_GAUSS_PLANE