!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRAINS AT NODAL POINTS FOR 4 AND 8 NODE RECTANGULAR ELEMENTS.
!  
!- INPUT ARGUMENTS:
!  I_ELEMENT    : THE ELEMENT NUMBER
!  NUM_ELE_NODE : THE NUMBER OF ELEMENT NODES
!
!- OUTPUT ARGUMENTS:
!  ADJ_ELE: THE TOTAL NUMBER OF ELEMENTS AROUND A NODE
!
!- CALL PROCEDURES:
!  EXTERNAL PROCEDURE: INV
!
!- CALLED BY
!  GET_STRAIN
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE STRAIN_NODAL_RECTANGLE(I_ELEMENT, NUM_ELE_NODE, ADJ_ELE)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, ELEMENT, NUM_NODE, ELEMENTS, ELEMENT_LIB, &
                         INTE_STRAIN, NODAL_STRAIN

IMPLICIT NONE

INTEGER(INT_KIND), INTENT(IN)::  I_ELEMENT                              ! THE ELEMENT NUMBER
INTEGER(INT_KIND), INTENT(IN)::  NUM_ELE_NODE                           ! THE NUMBER OF ELEMENT NODES
INTEGER(INT_KIND), INTENT(OUT):: ADJ_ELE(NUM_NODE)                      ! THE TOTAL NUMBER OF ELEMENTS AROUND A NODE
INTEGER(INT_KIND)                I, J, K                                ! LOOP INDEX
TYPE(ELEMENT), POINTER::         P_ELE_TYPE                             ! A POINTER TO ELEMENT TYPE
REAL(REAL_KIND)                  N(3, NUM_ELE_NODE * 3)                 ! SHAPE FUNCTION OF STRAINS
REAL(REAL_KIND)                  NN(NUM_ELE_NODE * 3, NUM_ELE_NODE * 3) ! COEFFICIENT MATRIX (SHAPE * SHAPE)
REAL(REAL_KIND)                  RH(NUM_ELE_NODE * 3)                   ! RIGHT HAND SIDE VECTOR (SHAPE * GAUSS_STRAIN)
REAL(REAL_KIND)                  ELE_NODAL_STRAIN(NUM_ELE_NODE * 3)     ! THE ELEMENT NODAL STRAIN

!-- REFERENCE THE ELEMENT TYPE --
P_ELE_TYPE => ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)

!-- CALCULATE STRAINS AT EACH NODAL POINT --
NN = 0.0
RH = 0.0

! SUM OVER ALL GAUSS POINTS
DO I = 1, P_ELE_TYPE%NUM_INTP
   ! CONSTRUCT SHAPE FUNCTION  
   N = 0.0
   DO J = 1, NUM_ELE_NODE
      DO K = 1, 3
         N(K,(J - 1) * 3 + K) = P_ELE_TYPE%SHAPE(J,I)
      ENDDO      
   ENDDO
      
   ! CALCULATE COEFFICIENT MATRIX
   NN = NN + MATMUL(TRANSPOSE(N), N) * P_ELE_TYPE%INTE_COORD(I)%WEIGHT  ! CONSTANT THICKNESS AND A ARE OMITTED
   
   ! CALCULATE THE RIGHT HAND SIDE VECTOR
   RH = RH + MATMUL(TRANSPOSE(N), INTE_STRAIN(I_ELEMENT)%VALUE((I - 1) * 3 + 1:I * 3)) &
           * P_ELE_TYPE%INTE_COORD(I)%WEIGHT
ENDDO   

! SOLVE THE NODAL STRAIN 王勖成《有限单元法》2003年版 P174 (5.3.18)
CALL INV(NN, NUM_ELE_NODE * 3)
ELE_NODAL_STRAIN = MATMUL(NN, RH)

!-- UPDATE THE GLOBAL NODAL STRAIN VECTOR --
DO I = 1, NUM_ELE_NODE
   J = ELEMENTS(I_ELEMENT)%NODE(I)
   NODAL_STRAIN(J)%VALUE = NODAL_STRAIN(J)%VALUE + ELE_NODAL_STRAIN((I - 1) * 3 + 1:I * 3)
   ADJ_ELE(J)            = ADJ_ELE(J) + 1
ENDDO

END SUBROUTINE STRAIN_NODAL_RECTANGLE