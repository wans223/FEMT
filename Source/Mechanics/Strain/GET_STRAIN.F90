!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRAINS AT INTEGRATION POINTS OR NODES.
!  
!- INPUT ARGUMENTS:
!  OPTION: 'GAUSS' OR 'NODAL' STRAIN
!
!- OUTPUT ARGUMENTS:
!  NONE
!
!- CALL PROCEDURES:
!  MODULE PROCEDURE  : ERROR
!  EXTERNAL PROCEDURE: STRAIN_GAUSS_PLANE, STRAIN_NODAL_PLANE
!
!- CALLED BY
!  SOLVER_MANAGER, GET_STRESS
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE GET_STRAIN(OPTION)

USE SOLUTION_DATA, ONLY: INT_KIND, WORD_KIND, NUM_NODE, NUM_ELEMENT, ELEMENT_LIB, &
                         ELEMENTS, NODAL_STRAIN, ERROR

IMPLICIT NONE

CHARACTER(WORD_KIND), INTENT(IN):: OPTION           ! 'GAUSS' OR 'NODAL' STRAIN
INTEGER(INT_KIND)                  I_ELEMENT        ! LOOP INDEX
INTEGER(INT_KIND)                  I_NODE           ! LOOP INDEX
INTEGER(INT_KIND)                  ERR              ! ERROR INDEX
INTEGER(INT_KIND), ALLOCATABLE::   ADJ_ELE(:)       ! THE TOTAL NUMBER OF ELEMENTS AROUND A NODE

!-- CALCULATE STRAINS AT GAUSS POINTS IN EACH ELEMENT --
DO I_ELEMENT = 1, NUM_ELEMENT
   SELECT CASE(TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE))
      CASE ('TRIANGLE3', 'TRIANGLE6', 'RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9')
         CALL STRAIN_GAUSS_PLANE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_NODE)

      CASE DEFAULT
         CALL ERROR('Invalid element type (' // TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE) // ') in GET_STRAIN!')
   END SELECT
ENDDO

!-- CALCULATE STRAINS AT NODES --
IF(TRIM(OPTION) .EQ. 'NODAL') THEN
   ! INITIATE DATA
   ALLOCATE(ADJ_ELE(NUM_NODE), STAT = ERR)
   IF(ERR .NE. 0) CALL ERROR('Failed to allocate ADJ_ELE!')
   ADJ_ELE = 0      
     
   ! CALCULATE ELEMENT NODAL STRAINS
   DO I_ELEMENT = 1, NUM_ELEMENT
      SELECT CASE(TRIM(ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%ELEMENT_TYPE))
         CASE ('TRIANGLE3', 'TRIANGLE6')
            CALL STRAIN_NODAL_TRIANGLE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_NODE, ADJ_ELE)
         
         CASE ('RECTANGLE4', 'RECTANGLE8', 'RECTANGLE9')
            CALL STRAIN_NODAL_RECTANGLE(I_ELEMENT, ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%NUM_NODE, ADJ_ELE)

      END SELECT
   ENDDO

   ! AVERAGE THE NODAL STRAIN OVER ADJECENT ELEMENTS
   DO I_NODE = 1, NUM_NODE
      NODAL_STRAIN(I_NODE)%VALUE = NODAL_STRAIN(I_NODE)%VALUE / ADJ_ELE(I_NODE)
   ENDDO
   
   DEALLOCATE(ADJ_ELE)
ENDIF

END SUBROUTINE GET_STRAIN