!***************************************************************************************************
!- PURPORSE:
!     CALCULATE STRAINS AT NODAL POINTS FOR 3 AND 6 NODE TRIANGULAR ELEMENTS.
!  
!- INPUT ARGUMENTS:
!  I_ELEMENT    : THE ELEMENT NUMBER
!  NUM_ELE_NODE : THE NUMBER OF ELEMENT NODES
!
!- OUTPUT ARGUMENTS:
!  ADJ_ELE: THE TOTAL NUMBER OF ELEMENTS AROUND A NODE
!
!- CALL PROCEDURES:
!  NONE
!
!- CALLED BY
!  GET_STRAIN
!
!- PROGAMMED BY:
!  ZHIHAI XIANG, DEPARTMENT OF ENGINEERING MECHANICS, TSINGHUA UNIVERSITY, JANUARY 18, 2016
!***************************************************************************************************

SUBROUTINE STRAIN_NODAL_TRIANGLE(I_ELEMENT, NUM_ELE_NODE, ADJ_ELE)

USE SOLUTION_DATA, ONLY: INT_KIND, REAL_KIND, NUM_NODE, ELEMENT_LIB, ELEMENTS, INTE_STRAIN, NODAL_STRAIN
                          
IMPLICIT NONE

INTEGER(INT_KIND), INTENT(IN)::  I_ELEMENT                            ! THE ELEMENT NUMBER
INTEGER(INT_KIND), INTENT(IN)::  NUM_ELE_NODE                         ! THE NUMBER OF ELEMENT NODES
INTEGER(INT_KIND), INTENT(OUT):: ADJ_ELE(NUM_NODE)                    ! THE TOTAL NUMBER OF ELEMENTS AROUND A NODE
INTEGER(INT_KIND)                I, J, K                              ! LOOP INDEX
REAL(REAL_KIND)                  N(3,3)                               ! SHAPE FUNCTION OF STRAINS
REAL(REAL_KIND)                  INV_N(3,3)                           ! THE INVERSE OF N MATRIX
REAL(REAL_KIND)                  DETERMINT                            ! |N|
REAL(REAL_KIND)                  RH(3)                                ! RIGHT HAND SIDE VECTOR (SHAPE * GAUSS_STRAIN)
REAL(REAL_KIND)                  ELE_NODAL_STRAIN(NUM_ELE_NODE * 3)   ! THE ELEMENT NODAL STRAIN

!-- THE STRAIN IS CONSTANT OVER THE 3-NODE TRIANGULAR ELEMENT --
IF(NUM_ELE_NODE .EQ. 3) THEN
   DO I = 1, NUM_ELE_NODE
      J = ELEMENTS(I_ELEMENT)%NODE(I)
      NODAL_STRAIN(J)%VALUE = NODAL_STRAIN(J)%VALUE + INTE_STRAIN(I_ELEMENT)%VALUE
      ADJ_ELE(J)            = ADJ_ELE(J) + 1
   ENDDO
   RETURN
ENDIF

!-- CALCULATE STRAINS AT EACH CORNER NODAL POINT --
DO I = 1, 3       ! STXX, STYY, STXY
   DO J = 1, 3    ! 3 INTEGRATION POINTS
      DO K = 1, 3 ! 3 NODES
         N(J,K) = ELEMENT_LIB(ELEMENTS(I_ELEMENT)%ELEMENT)%INTE_COORD(J)%COORD(K) ! LINEAR DISTRIBUTION
      ENDDO
    
      ! CALCULATED STRAINS AT INTEGRATION POINTS
      RH(J) = INTE_STRAIN(I_ELEMENT)%VALUE((J - 1) * 3 + I)
   ENDDO

   ! SOLVE THE NODEAL STRAIN OF THIS COMPONENT
   DETERMINT  = N(1,1) * N(2,2) * N(3,3) + N(2,1) * N(3,2) * N(1,3) + N(3,1) * N(1,2) * N(2,3) - &
                N(3,1) * N(2,2) * N(1,3) - N(3,2) * N(2,3) * N(1,1) - N(3,3) * N(1,2) * N(2,1)
   INV_N(1,1) = (N(2,2) * N(3,3) - N(3,2) * N(2,3)) / DETERMINT
   INV_N(1,2) = (N(3,1) * N(2,3) - N(2,1) * N(3,3)) / DETERMINT
   INV_N(1,3) = (N(2,1) * N(3,2) - N(3,1) * N(2,2)) / DETERMINT
   INV_N(2,1) = (N(3,2) * N(1,3) - N(1,2) * N(3,3)) / DETERMINT
   INV_N(2,2) = (N(1,1) * N(3,3) - N(3,1) * N(1,3)) / DETERMINT
   INV_N(2,3) = (N(3,1) * N(1,2) - N(1,1) * N(3,2)) / DETERMINT
   INV_N(3,1) = (N(1,2) * N(2,3) - N(2,2) * N(1,3)) / DETERMINT
   INV_N(3,2) = (N(2,1) * N(1,3) - N(1,1) * N(2,3)) / DETERMINT
   INV_N(3,3) = (N(1,1) * N(2,2) - N(2,1) * N(1,2)) / DETERMINT
   ELE_NODAL_STRAIN(I:I+6:3) = MATMUL(INV_N, RH)   ! EXTERPOLATE TO THE CORNER POINTS 王勖成《有限单元法》2003年版 P175 
ENDDO

!-- THE STRAIN IS LINEARLY DISTRIBUTED OVER THE 6-NODE TRIANGULAR ELEMENT --
ELE_NODAL_STRAIN(10:12) = (ELE_NODAL_STRAIN(1:3) + ELE_NODAL_STRAIN(4:6)) / 2.0
ELE_NODAL_STRAIN(13:15) = (ELE_NODAL_STRAIN(4:6) + ELE_NODAL_STRAIN(7:9)) / 2.0
ELE_NODAL_STRAIN(16:18) = (ELE_NODAL_STRAIN(7:9) + ELE_NODAL_STRAIN(1:3)) / 2.0

!-- UPDATE THE GLOBAL NODAL STRAIN VECTOR --
DO I = 1, NUM_ELE_NODE
   J = ELEMENTS(I_ELEMENT)%NODE(I)
   NODAL_STRAIN(J)%VALUE = NODAL_STRAIN(J)%VALUE + ELE_NODAL_STRAIN((I - 1) * 3 + 1:I * 3)
   ADJ_ELE(J)            = ADJ_ELE(J) + 1
ENDDO

END SUBROUTINE STRAIN_NODAL_TRIANGLE